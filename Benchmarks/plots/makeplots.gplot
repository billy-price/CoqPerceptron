set terminal pngcairo  background "#ffffff" fontscale 2 dashed dl 4 size 640, 520

set logscale x 2
set logscale y 2

set style line 1 lc rgb "#FF0005" lt 1 lw 4
set style line 2 lc rgb "#2C397D" lt 2 lw 4
set style line 3 lc rgb "#FF9900" lt 5 lw 6

set xtics rotate by 45 offset -1.5, -1.3 autofreq 50, 2
set key spacing 0.75 at graph 0.6, 0.96

set xlabel '#Vectors' font ",15"

set ylabel '#Epochs' offset 2 font ",15"
set output 'images/EpochPlot.png'
plot "vectors.plot" using 1:2 t "Epochs" with lines ls 1

set format y "%.2f"

set key at graph 0.68, 0.96

set ylabel 'Time (s)'
set output 'images/VectorsPlot.png'
plot "vectors.plot" using 1:9 t "Coq" with lines ls 2, \
"vectors.plot" using 1:6 t "CoqOpt" with lines ls 3, \
"vectors.plot" using 1:3 t "C++" with lines ls 1

set key at graph 0.6, 0.96

set xlabel '#Features'
set ylabel 'Time/Epoch (s)' offset 1
set output 'images/FeaturePlot.png'
plot "features.plot" using 1:10 t "Coq" with lines ls 2, \
"features.plot" using 1:7 t "CoqOpt" with lines ls 3, \
"features.plot" using 1:4 t "C++" with lines ls 1

set key at graph 0.6, 0.5

unset logscale y

set xlabel 'Feature Value Bound [-z,z]'
set ylabel 'Time/Epoch (s)'
set output 'images/ZPlot.png'
plot "Z.plot" using 1:10 t "Coq" with lines ls 2, \
"Z.plot" using 1:7 t "CoqOpt" with lines ls 3, \
"Z.plot" using 1:4 t "C++" with lines ls 1
